buildscript {
    repositories {
        mavenCentral()
        maven {
            url = uri("https://packages.confluent.io/maven/")
        }
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = uri("https://jitpack.io")
        }
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'com.github.eirnym.js2p' version '1.0'
    id "com.google.protobuf" version "0.8.15"
    id "com.github.imflog.kafka-schema-registry-gradle-plugin" version "1.1.1"
    id "com.github.davidmc24.gradle.plugin.avro" version "1.0.0"
}

sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

repositories {
    mavenCentral()
    maven {
        url = uri("https://packages.confluent.io/maven/")
    }

    maven {
        url = uri("https://jitpack.io")
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.15.3'
    implementation "org.apache.avro:avro:1.10.0"
    implementation "com.kjetland:mbknor-jackson-jsonschema_2.13:1.0.39"
    implementation 'javax.validation:validation-api:1.1.0.CR2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
    implementation "com.google.protobuf:protobuf-java:3.11.4"
    implementation 'com.github.javafaker:javafaker:1.0.2'

    implementation('org.apache.kafka:kafka-streams:2.7.0') {
        exclude group:'org.apache.kafka', module: 'kafka-clients'
    }
    implementation('org.apache.kafka:kafka-clients:2.7.0!!')
    implementation('io.confluent:kafka-streams-avro-serde:6.1.1') {
        exclude group:'org.apache.kafka', module: 'kafka-clients'
        exclude group:'org.apache.kafka', module: 'kafka-streams'
    }
    implementation('io.confluent:kafka-streams-protobuf-serde:6.1.1') {
        exclude group:'org.apache.kafka', module: 'kafka-clients'
        exclude group:'org.apache.kafka', module: 'kafka-streams'
    }

    implementation "io.confluent:kafka-avro-serializer:6.1.1"
    implementation "io.confluent:kafka-protobuf-serializer:6.1.1"
    implementation "io.confluent:kafka-json-serializer:6.1.1"
    implementation "io.confluent:kafka-json-schema-serializer:6.1.1"
    implementation "io.confluent:kafka-schema-registry-client:6.1.1"
    implementation "io.confluent:kafka-json-schema-provider:6.1.1"
    implementation "io.confluent:kafka-protobuf-provider:6.1.1"

    implementation "org.apache.logging.log4j:log4j-api:2.7"
    implementation "org.apache.logging.log4j:log4j-core:2.7"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.7"

    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation "org.apache.kafka:kafka-streams-test-utils:2.7.0"
    testImplementation "org.testcontainers:junit-jupiter:1.15.1"
    testImplementation "org.testcontainers:kafka:1.15.1"
    testImplementation "org.testcontainers:toxiproxy:1.15.1"
}

protobuf {
    generatedFilesBaseDir = "${project.buildDir}/generated-main-proto-java"

    protoc {
        artifact = 'com.google.protobuf:protoc:3.15.3'
    }
}

jsonSchema2Pojo {
    targetPackage = 'bbejeck.json.generated'
    generateBuilders = true
    annotationStyle = "JACKSON2"
    source = files("${project.projectDir}/src/main/json")
    targetDirectory = file("${project.buildDir}/generated-main-json-java")
    includeJsr303Annotations = false
    propertyWordDelimiters = ['_'] as char[]
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}


//Schema Registry section
schemaRegistry {
    url = 'http://localhost:8081'
// optional set these if you have Schema Regsitry set up for authentication
//    credentials {
//        username = 'basicauthentication-username'
//        password = 'basicauthentication-password'
//    }

    compatibility {
        subject('avro-avengers-value', 'streams/src/main/avro/avenger.avsc', 'AVRO')
    }

    // Possible types are ["JSON", "PROTOBUF", "AVRO"]
    register {
        subject('avro-avengers-value','streams/src/main/avro/avenger.avsc', 'AVRO')
        subject('person','streams/src/main/avro/person.avsc', 'AVRO')
        subject('college-value','streams/src/main/avro/college.avsc', 'AVRO')
                .addReference("bbejeck.chapter_3.avro.PersonAvro", "person", 1)
        subject('company-value','streams/src/main/avro/company.avsc', 'AVRO')
                .addReference("bbejeck.chapter_3.avro.PersonAvro", "person", 1)


        subject('truck_event','streams/src/main/avro/truck_event.avsc', 'AVRO')
        subject('plane_event','streams/src/main/avro/plane_event.avsc', 'AVRO')
        subject('delivery_event','streams/src/main/avro/delivery_event.avsc', 'AVRO')
        subject('inventory-events-value', 'streams/src/main/avro/all_events.avsc','AVRO')
                .addReference("bbejeck.chapter_3.avro.TruckEvent", "truck_event", 1)
                .addReference("bbejeck.chapter_3.avro.PlaneEvent", "plane_event", 1)
                .addReference("bbejeck.chapter_3.avro.DeliveryEvent", "delivery_event", 1)
    }

    config {
        subject('avro-avengers-value', 'BACKWARD')
    }



    task runAvroProducer(type: JavaExec){
        classpath sourceSets.main.runtimeClasspath
        main = "bbejeck.chapter_3.producer.avro.AvroProducer"
    }

    task runAvroConsumer(type: JavaExec){
        classpath sourceSets.main.runtimeClasspath
        main = "bbejeck.chapter_3.consumer.avro.AvroConsumer"
    }

    task runCompanyProducer(type: JavaExec) {
        classpath sourceSets.main.runtimeClasspath
        main = "bbejeck.chapter_3.producer.avro.ReferenceCompanyProducer"
    }

    task runCompanyConsumer(type: JavaExec) {
        classpath sourceSets.main.runtimeClasspath
        main = "bbejeck.chapter_3.consumer.avro.ReferenceCompanyConsumer"
    }

    task runCollegeProducer(type: JavaExec) {
        classpath sourceSets.main.runtimeClasspath
        main = "bbejeck.chapter_3.producer.avro.ReferenceCollegeProducer"
    }

    task runCollegeConsumer(type: JavaExec) {
        classpath sourceSets.main.runtimeClasspath
        main = "bbejeck.chapter_3.consumer.avro.ReferenceCollegeConsumer"
    }


}
